# Copyright (C) 2006-2018 Istituto Italiano di Tecnologia (IIT)
# Copyright (C) 2006-2010 RobotCub Consortium
# All rights reserved.
#
# This software may be modified and distributed under the terms of the
# BSD-3-Clause license. See the accompanying LICENSE file for details.

project(YARP_dev)

set(YARP_dev_HDRS include/yarp/dev/all.h
                  include/yarp/dev/api.h
                  include/yarp/dev/AudioGrabberInterfaces.h
                  include/yarp/dev/AudioVisualInterfaces.h
                  include/yarp/dev/CalibratorInterfaces.h
                  include/yarp/dev/CanBusInterface.h
                  include/yarp/dev/CartesianControl.h
                  include/yarp/dev/ControlBoardHelper.h
                  include/yarp/dev/ControlBoardHelpers.h
                  include/yarp/dev/ControlBoardInterfaces.h
                  include/yarp/dev/ControlBoardInterfacesImpl.h
                  include/yarp/dev/ControlBoardInterfacesImpl.inl
                  include/yarp/dev/ControlBoardPid.h
                  include/yarp/dev/DataSource.h
                  include/yarp/dev/DeviceDriver.h
                  include/yarp/dev/DriverLinkCreator.h
                  include/yarp/dev/Drivers.h
                  include/yarp/dev/FrameGrabberControl2.h
                  include/yarp/dev/FrameGrabberControl2Impl.h
                  include/yarp/dev/FrameGrabberInterfaces.h
                  include/yarp/dev/GazeControl.h
                  include/yarp/dev/GenericSensorInterfaces.h
                  include/yarp/dev/GPUInterface.h
                  include/yarp/dev/IAnalogSensor.h
                  include/yarp/dev/IBattery.h
                  include/yarp/dev/IControlLimits2.h
                  include/yarp/dev/IControlLimits2Impl.h
                  include/yarp/dev/IControlMode2.h
                  include/yarp/dev/IControlMode.h
                  include/yarp/dev/ICurrentControl.h
                  include/yarp/dev/IEncoders.h
                  include/yarp/dev/IEncodersTimed.h
                  include/yarp/dev/IHapticDevice.h
                  include/yarp/dev/IImpedanceControl.h
                  include/yarp/dev/IInteractionMode.h
                  include/yarp/dev/IInteractionModeImpl.h
                  include/yarp/dev/IJoypadController.h
                  include/yarp/dev/IMotorEncoders.h
                  include/yarp/dev/IMotor.h
                  include/yarp/dev/ImplementAxisInfo.h
                  include/yarp/dev/ImplementControlBoardInterfaces.h
                  include/yarp/dev/ImplementControlMode2.h
                  include/yarp/dev/ImplementControlMode.h
                  include/yarp/dev/ImplementCurrentControl.h
                  include/yarp/dev/ImplementEncodersTimed.h
                  include/yarp/dev/ImplementImpedanceControl.h
                  include/yarp/dev/ImplementMotorEncoders.h
                  include/yarp/dev/ImplementMotor.h
                  include/yarp/dev/ImplementPWMControl.h
                  include/yarp/dev/ImplementRemoteVariables.h
                  include/yarp/dev/ImplementTorqueControl.h
                  include/yarp/dev/IPidControl.h
                  include/yarp/dev/IPidControlImpl.h
                  include/yarp/dev/IPositionControl2.h
                  include/yarp/dev/IPositionControl2Impl.h
                  include/yarp/dev/IPositionControl.h
                  include/yarp/dev/IPositionDirect.h
                  include/yarp/dev/IPositionDirectImpl.h
                  include/yarp/dev/IPWMControl.h
                  include/yarp/dev/IRangefinder2D.h
                  include/yarp/dev/IRemoteVariables.h
                  include/yarp/dev/IRGBDSensor.h
                  include/yarp/dev/IRobotDescription.h
                  include/yarp/dev/ITorqueControl.h
                  include/yarp/dev/IVelocityControl2.h
                  include/yarp/dev/IVelocityControl2Impl.h
                  include/yarp/dev/IVelocityControl.h
                  include/yarp/dev/IVirtualAnalogSensor.h
                  include/yarp/dev/IVisualParams.h
                  include/yarp/dev/IVisualParamsImpl.h
                  include/yarp/dev/LaserMeasurementData.h
                  include/yarp/dev/IVisualServoing.h
                  include/yarp/dev/PolyDriverDescriptor.h
                  include/yarp/dev/PolyDriver.h
                  include/yarp/dev/PolyDriverList.h
                  include/yarp/dev/PreciselyTimed.h
                  include/yarp/dev/RemoteFrameGrabberDC1394.h
                  include/yarp/dev/SerialInterfaces.h
                  include/yarp/dev/ServiceInterfaces.h
                  include/yarp/dev/Wrapper.h)

if (CREATE_LIB_MATH)
  list(APPEND YARP_dev_HDRS include/yarp/dev/IFrameTransform.h
                            include/yarp/dev/ILocalization2D.h
                            include/yarp/dev/IMap2D.h
                            include/yarp/dev/INavigation2D.h
                            include/yarp/dev/Map2DLocation.h
                            include/yarp/dev/MapGrid2D.h)
endif()

set(YARP_dev_IMPL_HDRS )

set(YARP_dev_SRCS src/ControlBoardInterfacesImpl.cpp
                  src/ControlBoardPid.cpp
                  src/ControlCalibrationImpl.cpp
                  src/ControlMode2Impl.cpp
                  src/ControlModeImpl.cpp
                  src/CurrentControlImpl.cpp
                  src/DeviceDriver.cpp
                  src/Drivers.cpp
                  src/FrameGrabberControl2Impl.cpp
                  src/IAxisInfoImpl.cpp
                  src/IBattery.cpp
                  src/IControlLimit2sImpl.cpp
                  src/IEncodersTimedImpl.cpp
                  src/IInteractionModeImpl.cpp
                  src/IJoypadController.cpp
                  src/IMotorEncodersImpl.cpp
                  src/IMotorImpl.cpp
                  src/ImpedanceControlImpl.cpp
                  src/IPidControlImpl.cpp
                  src/IPositionControl2Impl.cpp
                  src/IPositionDirectImpl.cpp
                  src/IRangefinder2D.cpp
                  src/IRemoteVariablesImpl.cpp
                  src/IVelocityControl2Impl.cpp
                  src/IVisualParamsImpl.cpp
                  src/LaserMeasurementData.cpp
                  src/PolyDriver.cpp
                  src/PolyDriverDescriptor.cpp
                  src/PolyDriverList.cpp
                  src/PopulateDrivers.cpp
                  src/PWMControlImpl.cpp
                  src/RemoteCalibratorImpl.cpp
                  src/TorqueControlImpl.cpp)

if(CREATE_LIB_MATH)
  list(APPEND YARP_dev_SRCS src/IFrameTransform.cpp
                            src/IMap2D.cpp
                            src/MapGrid2D.cpp)
endif()

set(YARP_devices_SRCS src/devices/AnalogSensorClient/AnalogSensorClient.cpp
                      src/devices/AnalogWrapper/AnalogWrapper.cpp
                      src/devices/ControlBoardRemapper/ControlBoardRemapper.cpp
                      src/devices/ControlBoardRemapper/ControlBoardRemapperHelpers.cpp
                      src/devices/ControlBoardRemapper/RemoteControlBoardRemapper.cpp
                      src/devices/DeviceGroup/DeviceGroup.cpp
                      src/devices/DevicePipe/DevicePipe.cpp
                      src/devices/JoypadControlClient/JoypadControlClient.cpp
                      src/devices/JoypadControlServer/JoypadControlServer.cpp
                      src/devices/RemoteFrameGrabber/RemoteFrameGrabber.cpp
                      src/devices/RobotDescriptionClient/RobotDescriptionClient.cpp
                      src/devices/RobotDescriptionServer/RobotDescriptionServer.cpp
                      src/devices/ServerFrameGrabber/ServerFrameGrabber.cpp
                      src/devices/ServerSerial/ServerSerial.cpp
                      src/devices/TestMotor/TestMotor.cpp
                      src/devices/VirtualAnalogWrapper/VirtualAnalogWrapper.cpp)

set(YARP_devices_HDRS src/devices/AnalogSensorClient/AnalogSensorClient.h
                      src/devices/AnalogWrapper/AnalogWrapper.h
                      src/devices/ControlBoardRemapper/ControlBoardRemapper.h
                      src/devices/ControlBoardRemapper/ControlBoardRemapperHelpers.h
                      src/devices/ControlBoardRemapper/RemoteControlBoardRemapper.h
                      src/devices/DeviceGroup/DeviceGroup.h
                      src/devices/DevicePipe/DevicePipe.h
                      src/devices/JoypadControlClient/JoypadControlClient.h
                      src/devices/JoypadControlServer/JoypadControlNetUtils.h
                      src/devices/JoypadControlServer/JoypadControlServer.h
                      src/devices/RemoteFrameGrabber/RemoteFrameGrabber.h
                      src/devices/RobotDescriptionClient/RobotDescriptionClient.h
                      src/devices/RobotDescriptionServer/RobotDescriptionServer.h
                      src/devices/ServerFrameGrabber/ServerFrameGrabber.h
                      src/devices/ServerSerial/ServerSerial.h
                      src/devices/TestMotor/TestMotor.h
                      src/devices/VirtualAnalogWrapper/VirtualAnalogWrapper.h)

# handle the ros / yarp thrift messages
# the variables cbw2_msgs_srcs, cbw2_msgs_hrds and cbw2_msgs_incl_dirs will be set inside
add_subdirectory(src/devices/msgs)
# relative paths should be relative to current directory
unset(_tmp)
foreach(_var cbw2_msgs_srcs cbw2_msgs_hrds)
  foreach(_f ${${_var}})
    file(RELATIVE_PATH _f "${CMAKE_CURRENT_SOURCE_DIR}" "${_f}")
    list(APPEND _tmp "${_f}")
  endforeach()
  set(${_var} ${_tmp})
endforeach()



list(APPEND YARP_devices_SRCS ${cbw2_msgs_srcs})
list(APPEND YARP_devices_HDRS ${cbw2_msgs_hrds})
include_directories(${cbw2_msgs_incl_dirs})


# handle the source file for the controlboardwrapper2 device
# the variable cbw2_core_srcs, cbw2_core_hrds, cbw2_core_incl_dirs will be set inside
add_subdirectory(src/devices/ControlBoardWrapper)
list(APPEND YARP_devices_SRCS ${cbw2_core_srcs})
list(APPEND YARP_devices_HDRS ${cbw2_core_hrds})
include_directories(${cbw2_core_incl_dirs})


# handle the source file for the RemoteControlBoard device
# the variables rmc_core_srcs and rmc_core_hrds and rmc_core_incl_dirs will be set inside
add_subdirectory(src/devices/RemoteControlBoard)
list(APPEND YARP_devices_SRCS ${rmc_core_srcs})
list(APPEND YARP_devices_HDRS ${rmc_core_hrds})
include_directories(${rmc_core_incl_dirs})

# add ServerInertial device
add_subdirectory(src/devices/ServerInertial)
list(APPEND YARP_devices_SRCS ${serverInertial_srcs}
                              ${inertial_msgs_srcs})
list(APPEND YARP_devices_HDRS ${serverInertial_hdrs}
                              ${inertial_msgs_hdrs})
include_directories(${serverInertial_incl_dirs})

# add ServerGrabber device
add_subdirectory(src/devices/ServerGrabber)
list(APPEND YARP_devices_SRCS ${serverGrabber_srcs}
                              ${grabber_msgs_srcs})
list(APPEND YARP_devices_HDRS ${serverGrabber_hdrs}
                              ${grabber_msgs_hdrs})
include_directories(${serverGrabber_incl_dirs})

# add RGBDSensorClient device
add_subdirectory(src/devices/RGBDSensorClient)
list(APPEND YARP_devices_SRCS ${RGBDClient_srcs}
                              ${RGBDSensor_msgs_srcs})
list(APPEND YARP_devices_HDRS ${RGBDClient_hrds}
                              ${RGBDSensor_msgs_hdrs})
include_directories(${RGBDClient_incl_dirs})

# add DepthSensorWrapper device
add_subdirectory(src/devices/RGBDSensorWrapper)
list(APPEND YARP_devices_SRCS ${RGBDWrap_srcs}
                              ${RGBDSensor_msgs_srcs})
list(APPEND YARP_devices_HDRS ${RGBDWrap_hdrs}
                              ${RGBDSensor_msgs_hdrs})
include_directories(${RGBDWrap_incl_dirs})


source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}"
             PREFIX "Source Files"
             FILES ${YARP_dev_SRCS})
source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}"
             PREFIX "Header Files"
             FILES ${YARP_dev_HDRS}
                   ${YARP_dev_IMPL_HDRS})
source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}"
             PREFIX "Devices Files"
             FILES ${YARP_devices_SRCS}
                   ${YARP_devices_HDRS})

set_property(GLOBAL APPEND PROPERTY YARP_TREE_INCLUDE_DIRS "${PROJECT_SOURCE_DIR}/include")
get_property(YARP_TREE_INCLUDE_DIRS GLOBAL PROPERTY YARP_TREE_INCLUDE_DIRS)

if (CREATE_LIB_MATH)
  add_definitions(-DWITH_YARPMATH)
endif()

include_directories(${CMAKE_CURRENT_SOURCE_DIR}/src/devices/AnalogSensorClient
                    ${CMAKE_CURRENT_SOURCE_DIR}/src/devices/JoypadControlServer)

include_directories(${YARP_TREE_INCLUDE_DIRS})
include_directories(SYSTEM ${ACE_INCLUDE_DIRS})

add_library(YARP_dev ${YARP_dev_SRCS}
                     ${YARP_dev_HDRS}
                     ${YARP_dev_IMPL_HDRS}
                     ${YARP_devices_SRCS}
                     ${YARP_devices_HDRS})
add_library(YARP::YARP_dev ALIAS YARP_dev)

target_include_directories(YARP_dev PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
                                           $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>)

target_link_libraries(YARP_dev PRIVATE YARP::YARP_OS
                                       YARP::YARP_rosmsg
                               PUBLIC YARP::YARP_sig)

if (CREATE_LIB_MATH)
  target_link_libraries(YARP_dev PRIVATE YARP::YARP_math)
endif()

if(NOT CMAKE_MINIMUM_REQUIRED_VERSION VERSION_LESS 3.1)
  message(AUTHOR_WARNING "CMAKE_MINIMUM_REQUIRED_VERSION is now ${CMAKE_MINIMUM_REQUIRED_VERSION}. This check can be removed.")
endif()
if(CMAKE_VERSION VERSION_LESS 3.1)
  if(DEFINED CXX11_FLAGS)
    target_compile_options(YARP_dev PUBLIC ${CXX11_FLAGS})
  endif()
else()
  target_compile_features(YARP_dev PUBLIC cxx_nullptr
                                          cxx_override
                                          cxx_final)
endif()

set_property(TARGET YARP_dev PROPERTY PUBLIC_HEADER ${YARP_dev_HDRS})
set_property(TARGET YARP_dev PROPERTY PRIVATE_HEADER ${YARP_dev_IMPL_HDRS})

if(YARP_HAS_ACE)
  target_link_libraries(YARP_dev PRIVATE ${ACE_LIBRARIES})
endif(YARP_HAS_ACE)

install(TARGETS YARP_dev
        EXPORT YARP
        COMPONENT runtime
        RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}"
        LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}"
        ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}"
        PUBLIC_HEADER DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/yarp/dev"
        PRIVATE_HEADER DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/yarp/dev/impl")

set_property(GLOBAL APPEND PROPERTY YARP_LIBS YARP_dev)
set_property(TARGET YARP_dev PROPERTY INCLUDE_DIRS ${YARP_TREE_INCLUDE_DIRS})
set_property(TARGET YARP_dev PROPERTY FOLDER "Libraries")
