# Copyright (C) 2006-2018 Istituto Italiano di Tecnologia (IIT)
# All rights reserved.
#
# This software may be modified and distributed under the terms of the
# BSD-3-Clause license. See the accompanying LICENSE file for details.

set(headers)
set(sources)

set(prefix)
if ("@family@" STREQUAL "thrift")
  string(REGEX REPLACE "^/*([^/].+[^/])/*$" "\\1/" prefix "@include_prefix@")
endif()

file(STRINGS "@dir@/@yarpidlName@_indexALL.txt" index)
foreach(genFile ${index})
  get_filename_component(type ${genFile} EXT)
  get_filename_component(main ${genFile} NAME_WE)
  if(${type} STREQUAL ".h")
    if(EXISTS "@dir@/${genFile}")
      configure_file("@dir@/${genFile}"
                     "@output_dir@/include/${prefix}${genFile}"
                     COPYONLY)
      list(APPEND headers "include/${prefix}${genFile}")
      list(REMOVE_DUPLICATES headers)
    else()
      message(WARNING "${genFile} not found in @yarpidlName@ dir!")
    endif()
    if(EXISTS "@dir@/${main}Reply${type}")
      configure_file("@dir@/${main}Reply${type}"
                     "@output_dir@/include/${prefix}${main}Reply${type}"
                     COPYONLY)
      list(APPEND headers "include/${prefix}${main}Reply${type}")
      list(REMOVE_DUPLICATES headers)
    endif()
  elseif(${type} STREQUAL ".cpp")
    if(EXISTS "@dir@/${genFile}")
      configure_file("@dir@/${genFile}"
                     "@output_dir@/src/${genFile}"
                     COPYONLY)
      list(APPEND sources "src/${genFile}")
      list(REMOVE_DUPLICATES sources)
    else()
      message(WARNING "${genFile} not found in @yarpidlName@ dir!")
    endif()

  else()
    message(WARNING "Filename extension of ${genFile} is neither .h nor .cpp")
  endif()
endforeach(genFile)


file(WRITE "@output_dir@/@yarpidl_target_name@.cmake"
"## This is an automatically-generated file.
## It could get re-generated if the ALLOW_IDL_GENERATION flag is on

set(headers ${headers})
set(sources ${sources})
")
